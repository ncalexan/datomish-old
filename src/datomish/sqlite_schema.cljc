;; This Source Code Form is subject to the terms of the Mozilla Public
;; License, v. 2.0. If a copy of the MPL was not distributed with this
;; file, You can obtain one at http://mozilla.org/MPL/2.0/.

(ns datomish.sqlite-schema
  #?(:cljs
     (:require-macros
      [datomish.pair-chan :refer [go-pair <?]]
      [cljs.core.async.macros :refer [go]]))
  #?(:clj
     (:require
      [datomish.sqlite :as s]
      [datomish.pair-chan :refer [go-pair <?]]
      [clojure.core.async :refer [go <! >!]])
     :cljs
     (:require
      [datomish.sqlite :as s]
      [datomish.pair-chan]
      [cljs.core.async :as a :refer [<! >!]])))

(def current-version 1)

(def v1-statements
  ["CREATE TABLE datoms (e INTEGER NOT NULL, a TINYINT NOT NULL, v BLOB NOT NULL, tx INTEGER NOT NULL, index_avet TINYINT NOT NULL DEFAULT 0, index_vaet TINYINT NOT NULL DEFAULT 0)"
   "CREATE TABLE transactions (e INTEGER NOT NULL, a TINYINT NOT NULL, v BLOB NOT NULL, tx INTEGER NOT NULL, added TINYINT NOT NULL DEFAULT 1)"
   "CREATE INDEX eavt ON datoms (e, a)" ;; No v -- that's an opt-in index.
   "CREATE INDEX aevt ON datoms (a, e)" ;; No v -- that's an opt-in index.
   "CREATE INDEX avet ON datoms (a, v, e) WHERE index_avet = 1" ;; Opt-in index: only if a has :db/index true.
   "CREATE INDEX vaet ON datoms (v, a, e) WHERE index_vaet = 1" ;; Opt-in index: only if a has :db/valueType :db.type/ref
   "CREATE INDEX tx ON transactions (tx)"])

(defn create-current-version
  [db]
  (go-pair
    (doseq [statement v1-statements]
      (<? (s/execute! db [statement])))
    (<? (s/set-user-version db current-version))
    (<? (s/get-user-version db))))

(defn update-from-version
  [db from-version]
  {:pre [(> from-version 0)]} ;; Or we'd create-current-version instead.
  {:pre [(< from-version current-version)]} ;; Or we wouldn't need to update-from-version.
  (go-pair
    (throw (Exception. "No possible migrations!")) ;; TODO: raise.
    (<? (s/set-user-version db current-version))
    (<? (s/get-user-version db))))

(defn ensure-current-version
  [db]
  (go-pair
    (let [v (<? (s/get-user-version db))]
      (cond
        (= v current-version)
        v

        (= v 0)
        (<? (create-current-version db))

        (< v current-version)
        (<? (update-from-version db v))))))
